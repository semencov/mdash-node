Tret = require "../tret"
Lib = require '../lib'

module.exports = class Abbr extends Tret

  order: 7

  rules:

    # Расстановка пробелов перед сокращениями dpi, lpi
    nobr_abbreviation:
      pattern: [
        /(\s+|^|\>)(\d+)(\s|\t)*(dpi|lpi)([\s\;\.\?\!\:\(]|$)/ig
      ]
      replacement: [
        ($) -> "#{$[1]}#{$[2]}&nbsp;#{$[4]}#{$[5]}"
      ]

    # Расстановка пробелов перед сокращениями гл., стр., рис., илл., ст., п.
    nobr_acronym:
      pattern: [
        /(\s|^|\>|\()(гл|стр|рис|илл?|ст|п|с)\.(\s|\t)*(\d+)(\&nbsp\;|\s|\.|\,|\?|\!|\)|$)/ig
      ]
      replacement: [
        ($) -> "#{$[1]}#{$[2]}.&nbsp;#{$[4]}#{$[5]}"
      ]

    # Расстановка пробелов перед сокращениями см., им.
    nobr_sm_im:
      pattern: [
        /(\s|^|\>|\()(см|им)\.(\s|\t)*([а-яё0-9a-z]+)(\s|\.|\,|\?|\!|\)|$)/ig
      ]
      replacement: [
        ($) -> "#{$[1]}#{$[2]}.&nbsp;#{$[4]}#{$[5]}"
      ]

    # Расстановка пробелов в сокращениях г., ул., пер., д.
    nobr_locations:
      pattern: [
        /(\s|^|\>)(г|ул|пер|просп|пл|бул|наб|пр|ш|туп)\.(\s|\t)*([а-яё0-9a-z]+)(\s|\.|\,|\?|\!|$)/ig
        /(\s|^|\>)(б\-р|пр\-кт)(\s|\t)*([а-яё0-9a-z]+)(\s|\.|\,|\?|\!|$)/ig
        /(\s|^|\>)(д|кв|эт)\.(\s|\t)*(\d+)(\s|\.|\,|\?|\!|$)/ig
      ]
      replacement: [
        ($) -> "#{$[1]}#{$[2]}.&nbsp;#{$[4]}#{$[5]}"
        ($) -> "#{$[1]}#{$[2]}&nbsp;#{$[4]}#{$[5]}"
        ($) -> "#{$[1]}#{$[2]}.&nbsp;#{$[4]}#{$[5]}"
      ]

    # Замена символов и привязка сокращений в размерных величинах: м, см, м2.
    nbsp_before_unit:
      pattern: [
        /(\s|^|\>|\&nbsp\;|\,)(\d+)( |\&nbsp\;)?(м|мм|см|дм|км|гм|km|dm|cm|mm)(\s|\.|\!|\?|\,|$|\&plusmn\;|\;)/ig
        /(\s|^|\>|\&nbsp\;|\,)(\d+)( |\&nbsp\;)?(м|мм|см|дм|км|гм|km|dm|cm|mm)([32]|&sup3;|&sup2;)(\s|\.|\!|\?|\,|$|\&plusmn\;|\;)/ig
      ]
      replacement: [
        ($) -> "#{$[1]}#{$[2]}&nbsp;#{$[4]}#{$[5]}"
        ($) -> "#{$[1]}#{$[2]}&nbsp;#{$[4]}" + (if $[5] is "3" or $[5] is "2" then "&sup#{$[5]};" else $[5]) + $[6]
      ]

    # Замена символов и привязка сокращений в весовых величинах: г, кг, мг.
    nbsp_before_weight_unit:
      pattern: [
        /(\s|^|\>|\&nbsp\;|\,)(\d+)( |\&nbsp\;)?(г|кг|мг|т)(\s|\.|\!|\?|\,|$|\&nbsp\;|\;)/ig
      ]
      replacement: [
        ($) -> "#{$[1]}#{$[2]}&nbsp;#{$[4]}#{$[5]}"
      ]

    # Установка пробельных символов в сокращении вольт
    nobr_before_unit_volt:
      pattern: [
        /(\d+)([вВ]| В)(\s|\.|\!|\?|\,|$)/g
      ]
      replacement: [
        ($) -> "#{$[1]}&nbsp;В#{$[3]}"
      ]

    # Объединение сокращений P.S., P.P.S.
    ps_pps:
      pattern: [
        /(^|\s|\>|\r|\n)(p\.\s?)(p\.\s?)?(s\.)([^\<])/ig
      ]
      replacement: [
        ($) -> $[1] + Lib.tag("#{$[2].trim()} " + (if $[3]? then "#{$[3].trim()} " else "") + $[4], "nobr") + $[5]
      ]

    # Объединение сокращений и т.д., и т.п., в т.ч.
    nobr_vtch_itd_itp:
      pattern: [
        /(^|\s|\&nbsp\;)и( |\&nbsp\;)т\.?[ ]?д(\.|\,|$|\s|\&nbsp\;)/g
        /(^|\s|\&nbsp\;)и( |\&nbsp\;)т\.?[ ]?п(\.|\,|$|\s|\&nbsp\;)/g
        /(^|\s|\&nbsp\;)в( |\&nbsp\;)т\.?[ ]?ч(\.|\,|$|\s|\&nbsp\;)/g
      ]
      replacement: [
        ($) -> $[1] + Lib.tag("и т. д.", "nobr") + (if $[3] isnt "." then $[3] else "")
        ($) -> $[1] + Lib.tag("и т. п.", "nobr") + (if $[3] isnt "." then $[3] else "")
        ($) -> $[1] + Lib.tag("в т. ч.", "nobr") + (if $[3] isnt "." then $[3] else "")
      ]

    # Обработка т.е.
    nbsp_te:
      pattern: [
        /(^|\s|\&nbsp\;)([тТ])\.?[ ]?е\./g
      ]
      replacement: [
        ($) -> $[1] + Lib.tag("#{$[2]}. е.", "nobr")
      ]

    # Форматирование денежных сокращений (расстановка пробелов и привязка названия валюты к числу)
    nbsp_money_abbr:
      pattern: [
        /(\d)((\s|\&nbsp\;)?(тыс|млн|млрд)\.?(\s|\&nbsp\;)?)?(\s|\&nbsp\;)?(руб\.|долл\.|евро|€|&euro;|\$|у[\.]? ?е[\.]?)/g
      ]
      replacement: [
        ($) -> $[1] + (if $[4]? then "&nbsp;#{$[4]}" + (if $[4] is "тыс" then "." else "") else "") + "&nbsp;" + (if not $[7].match(/у[\\\\.]? ?е[\\\\.]?/gi) then $[7] else "у.е.")
      ]

    # Привязка сокращений форм собственности к названиям организаций
    nbsp_org_abbr:
      pattern     : [
        /([^a-zA-Zа-яёА-ЯЁ]|^)(ООО|ЗАО|ОАО|НИИ|ПБОЮЛ) ([a-zA-Zа-яёА-ЯЁ]|\"|\&laquo\;|\&bdquo\;|<)/ig
        /([^a-zA-Zа-яёА-ЯЁ]|^)(SIA|VAS|AAS|AS|IK) ([a-zA-Zа-яёА-ЯЁ]|\"|\&laquo\;|\&bdquo\;|<)/ig  # правило для Латвии
      ]
      replacement : [
        ($) -> "#{$[1]}#{$[2]}&nbsp;#{$[3]}"
        ($) -> "#{$[1]}#{$[2]}&nbsp;#{$[3]}"  # правило для Латвии
      ]

    # Привязка сокращения ГОСТ к номеру
    nobr_gost:
      pattern: [
        /(\s|\&nbsp\;|^)ГОСТ( |\&nbsp\;)?(\d+\.?\d*)((\-|\&minus\;|\&mdash\;)(\d+))?(( |\&nbsp\;)(\-|\&mdash\;))?/ig
        /(\s|\&nbsp\;|^|\>)ГОСТ( |\&nbsp\;)?(\d+\.?\d*)(\-|\&minus\;|\&mdash\;)(\d+)/ig
        /(\s|\&nbsp\;|^|\>)LVS( |\&nbsp\;)?(\d+)(\:|\-|\&minus\;|\&mdash\;|)(\d+)/ig  # правило для Латвии
        /(\b)(RFC|ISO|IEEE)( |\&nbsp\;)?(\d+[\.\-\/\:]?\d*)/ig  # правило для международных стандартов
      ]
      replacement: [
        ($) -> $[1] + Lib.tag("ГОСТ #{$[3]}" + (if $[6]? then "&ndash;#{$[6]}" else "") + (if $[7]? then " &mdash;" else ""), "nobr")
        ($) -> "#{$[1]}ГОСТ #{$[3]}&ndash;#{$[5]}"
        ($) -> $[1] + Lib.tag("LVS #{$[3]}:#{$[5]}", "nobr")  # правило для Латвии
        ($) -> $[1] + Lib.tag("#{$[2]} #{$[4]}", "nobr")  # правило для международных стандартов
      ]


